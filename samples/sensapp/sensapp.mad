#
# SensApp architecture
#

service Dispatcher {

    settings {
        queue: FIFO
        autoscaling {
            period: 10
            limits: [1, 20]
        }
    }

    operation dispatch {
        retry (delay: exponential(5), limit: 10) {
            query Registry/check
            invoke Storage/store
        }
        ignore {
            invoke Notifier/notify
        }
    }

}

service Registry {

    operation check {
        think 2
    }

}

service Storage {

    settings {
        queue: FIFO
        autoscaling {
            period: 10
            limits: [1, 20]
        }
    }

    operation store {
        retry(limit: 20, delay:exponential(5)) {
            query DB/insert {timeout:10, priority: 10}
        }
    }

    operation search {
        query DB/select {priority: 5}
    }
}

service Notifier {

    settings {
        queue: FIFO
    }

    operation notify {
        query Storage/search
        invoke Followers/found
    }

}

# Stubs to close the architecture

client Sensors {
    every 1 {
        invoke Dispatcher/dispatch
    }
}

client WebUI {
    every 20 {
        query Storage/search {priority: 5}
        query Storage/search {priority: 5}
        query Storage/search {priority: 5}
    }
}

service Followers {
    operation found {
        think 5
    }
}

service DB {

    settings {
        queue: LIFO
        throttling: tail-drop(100)
        autoscaling {
            period: 20
            limits: [4, 5]
        }
    }

    operation insert {
        think 4
    }

    operation select {
        think 20
    }

}